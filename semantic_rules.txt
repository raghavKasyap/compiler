<program> ===> <otherFunctions> <mainFunction>
<mainFunction> ===> TK_MAIN <stmts> TK_END
<otherFunctions> ===> <function> <otherFunctions>
<otherFunctions> ===> ϵ
<function> ===> TK_FUNID <input_par> <output_par> TK_SEM <stmts> TK_END
<input_par> ===> TK_INPUT TK_PARAMETER TK_LIST TK_SQL <parameter_list> TK_SQR
<output_par> ===> TK_OUTPUT TK_PARAMETER TK_LIST TK_SQL <parameter_list> TK_SQR
<output_par> ===> ϵ
<parameter_list> ===> <dataType> TK_ID <remaining_list>
<dataType> ===> <primitiveDatatype>
<dataType> ===> <constructiveDatatype>
<primitiveDatatype> ===> TK_INT
<primitiveDatatype> ===> TK_REAL
<constructiveDatatype> ===> TK_RECORD TK_RUID
<constructiveDatatype> ===> TK_UNION TK_RUID
<constructiveDatatype> ===> TK_RUID
<remaining_list> ===> TK_COMMA <parameter_list>
<remaining_list> ===> ϵ
<stmts> ===> <typeDefinitions> <declarations> <otherStmts> <returnStmt>
<typeDefinitions> ===> <actualOrRedefined> <typeDefinitions>
<typeDefinitions> ===> ϵ
<actualOrRedefined> ===> <typeDefinition>
<actualOrRedefined> ===> <definetypestmt>
<typeDefinition> ===> TK_RECORD TK_RUID <fieldDefinitions> TK_ENDRECORD
<typeDefinition> ===> TK_UNION TK_RUID <fieldDefinitions> TK_ENDUNION
<fieldDefinitions> ===> <fieldDefinition> <fieldDefinition> <moreFields>
<fieldDefinition> ===> TK_TYPE <fieldType> TK_COLON TK_FIELDID TK_SEM
<fieldType> ===> <primitiveDatatype> 
<fieldType> ===> TK_RUID
<moreFields> ===> <fieldDefinition> <moreFields>
<moreFields> ===> ϵ
<declarations> ===> <declaration> <declarations>
<declarations> ===> ϵ
<declaration> ===> TK_TYPE <dataType> TK_COLON TK_ID <global_or_not> TK_SEM
<global_or_not> ===> TK_COLON TK_GLOBAL
<global_or_not> ===> ϵ
<otherStmts> ===> <stmt> <otherStmts>
<otherStmts> ===> ϵ
<stmt> ===> <assignmentStmt>
<stmt> ===> <iterativeStmt>
<stmt> ===> <conditionalStmt>
<stmt> ===> <ioStmt>
<stmt> ===> <funCallStmt>
<assignmentStmt> ===> <singleOrRecId> TK_ASSIGNOP <arithmeticExpression> TK_SEM
<singleOrRecId> ===> TK_ID <constructedVariable>
<constructedVariable> ===> <oneExpansion> <moreExpansions>
<constructedVariable> ===> ϵ
<oneExpansion> ===> TK_DOT TK_FIELDID
<moreExpansions> ===> <oneExpansion> <moreExpansions>
<moreExpansions> ===> ϵ
<funCallStmt> ===> <outputParameters> TK_CALL TK_FUNID TK_WITH TK_PARAMETERS <inputParameters> TK_SEM
<outputParameters> ===> TK_SQL <idList> TK_SQR TK_ASSIGNOP
<outputParameters> ===> ϵ
<inputParameters> ===> TK_SQL <idList> TK_SQR
<iterativeStmt> ===> TK_WHILE TK_OP <booleanExpression> TK_CL <stmt> <otherStmts> TK_ENDWHILE
<conditionalStmt> ===> TK_IF TK_OP <booleanExpression> TK_CL TK_THEN <stmt> <otherStmts> <elsecondition>
<elsecondition> ===> TK_ELSE <stmt> <otherStmts> TK_ENDIF
<elsecondition> ===> TK_ENDIF
<ioStmt> ===> TK_READ TK_OP <var> TK_CL TK_SEM
<ioStmt> ===> TK_WRITE TK_OP <var> TK_CL TK_SEM
<arithmeticExpression> ===> <term> <expPrime>
<expPrime> ===> <lowPrecedenceOperators> <term> <expPrime>
<expPrime> ===> ϵ
<term> ===> <factor> <termPrime>
<termPrime> ===> <highPrecedenceOperators><factor> <termPrime>
<termPrime> ===> ϵ
<factor> ===> TK_OP <arithmeticExpression> TK_CL 
<factor> ===> <var>
<highPrecedenceOperator>===> TK_MUL
<highPrecedenceOperator> ===> TK_DIV
<lowPrecedenceOperators> ===> TK_PLUS
<lowPrecedenceOperators> ===> TK_MINUS
<booleanExpression> ===> TK_OP <booleanExpression> TK_CL <logicalOp> TK_OP <booleanExpression> TK_CL
<booleanExpression> ===> <var> <relationalOp> <var>
<booleanExpression> ===> TK_NOT TK_OP <booleanExpression> TK_CL
<var> ===> <singleOrRecId>
<var> ===> TK_NUM
<var> ===> TK_RNUM
<logicalOp> ===> TK_AND
<logicalOp> ===> TK_OR
<relationalOp> ===> TK_LT
<relationalOp> ===> TK_LE
<relationalOp> ===> TK_EQ
<relationalOp> ===> TK_GT
<relationalOp> ===> TK_GE
<relationalOp> ===> TK_NE
<returnStmt> ===> TK_RETURN <optionalReturn> TK_SEM
<optionalReturn> ===> TK_SQL <idList> TK_SQR
<optionalReturn> ===> ϵ
<idList> ===> TK_ID <more_ids>
<more_ids> ===> TK_COMMA <idList>
<more_ids> ===> ϵ
<definetypestmt> ===> TK_DEFINETYPE <A> TK_RUID TK_AS TK_RUID
<A> ===> TK_RECORD
<A> ===> TK_UNION

